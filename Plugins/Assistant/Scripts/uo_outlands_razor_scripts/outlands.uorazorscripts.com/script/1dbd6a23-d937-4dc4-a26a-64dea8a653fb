# This is an automated backup - check out https://outlands.uorazorscripts.com/script/1dbd6a23-d937-4dc4-a26a-64dea8a653fb for latest
# Automation by Jaseowns
## Script: Long Duration Buffs Hotkey
## Created by: Jaseowns#6694
#############################################

# Long Duration Buffs Hotkey by Jaseowns
# UO Outlands

@setvar! minimumWaitForPing 200

# Eat food if we got it
if not findbuff "food" and findtype "tray" backpack as jood
    overhead "Eating Food" 88
    dclick jood
    wait minimumWaitForPing
endif

# Use Taste ID if possible
if skill "Taste Identification" >= 50 and not findbuff "herb"
    useskill 'tasteidentification'
    wft 500
    target backpack
endif

if skill "Camping" > 0
    if not findbuff "campfire"
        overhead "Camping Buff" 88
        while not findtype "campfire" ground -1 -1 2 and findtype "kindling" as jindling
            dclick jindling
            wait minimumWaitForPing
        endwhile
        if not findtype "campfire" ground -1 -1 2
            overhead "We have camping, but can not make a campfire!" 34
        else
            while findtype "campfire" ground -1 -1 2 and not findbuff "campfire"
                overhead "Waiting for a campfire buff..." 88
                wait 1000
            endwhile
        endif
    endif
endif

clearsysmsg 

if skill "Tracking" > 0
    if not findbuff "tracking"
        while not gumpexists 4267467659
            useskill 'tracking'
            wait minimumWaitForPing
        endwhile
        if gumpexists 4267467659
            while not insysmsg "You will now hunt all hostile players."
                gumpresponse 8 4267467659
                waitforgump 4267467659 5000
            endwhile
            gumpresponse 6 4267467659
            wait minimumWaitForPing
        endif
        gumpclose 4267467659
    endif
endif