# This is an automated backup - check out https://outlands.uorazorscripts.com/script/a89337e1-c773-43b7-bd67-ac858cdef901 for latest
# Automation by Jaseowns
## Script: Siv's Taming Trainer
## Created by: Siv#1881
#############################################

//Sivs Taming Trainer v1.0
//Train Animal Taming 50-120
//------------------------------------//
//Description: Taming Trainer         //
//Credits to: Zell, a rideable llama  //
//Author: Siv                                   //
//Date: Sep/24/2021                      //
//Version: 1.0                                 //
//-----------------------------------//
//
//-------------------------------//
//Requirements:                      //
//  Vet Skill                              //
//  Bandages                           //
//  Tank Pet                             //
//------------------------------//
//------------------//
//      Init               //
//------------------//
//Create a timer for vet
if not timerexists 'vet'
    createtimer 'vet'
    settimer 'vet' 10000
endif
//Create a timer for taming
if not timerexists 'taming'
    createtimer 'taming'
    settimer 'taming' 13000
endif
//Create a variable for tankpet
if not varexist 'tankpet'
    setvar 'tankpet' 0
endif
//Set tankpet on first run
if not find 'tankpet'
    overhead "Target your tank pet!" 555
    setvar 'tankpet'
    //Ignore tank pet if same as tamable type
    ignore 'tankpet'
endif
//Safety Measure
hotkey "clear target queue"
//Prevent taming interruptions
warmode 'on'
warmode 'off'
//Check for bandages
if not findtype 'clean bandage%s%' backpack
    overhead "You need to get some bandages!" 555
    stop
endif
//------------------//
//     Heal Tank    //
//------------------//
if find 'tankpet' ground any any 2 and timer 'vet' > 10000
    if findtype 'clean bandage%s%' backpack
        overhead "Bandaging!" 555
        dclicktype 'clean bandage%s%' backpack
        waitfortarget 1000
        target 'tankpet'
        settimer 'vet' 0
    endif
endif
//Safety measure
hotkey 'cancel current target'
//--------------------//
//  Setup Tame List  //
//     From Zell         //
//--------------------//
// Apply pets to tame at specific levels feel free to add more
if skill 'Taming' < 60
    removelist 'tames'
    createlist 'tames'
    //Frog
    pushlist 'tames' '80'
elseif skill 'Taming' < 70
    removelist 'tames'
    createlist 'tames'
    //Cave Bear
    pushlist 'tames' '213'
elseif skill 'Taming' < 75
    removelist 'tames'
    createlist 'tames'
    //Scarab
    pushlist 'tames' '169'
elseif skill 'Taming' < 80
    removelist 'tames'
    createlist 'tames'
    //Drake Whelp
    pushlist 'tames' '718'
    //Aegis Slime
    pushlist 'tames' '51'
elseif skill 'Taming' < 85
    removelist 'tames'
    createlist 'tames'
    //Deep Crawler
    pushlist 'tames' '315'
    //Bullvore
    pushlist 'tames' '715'
elseif skill 'Taming' < 90
    removelist 'tames'
    createlist 'tames'
    //Drake red
    pushlist 'tames' '61'
    //Drake grey
    pushlist 'tames' '60'
elseif skill 'Taming' < 95
    removelist 'tames'
    createlist 'tames'
    //Husk Crab
    pushlist 'tames' '729'
    //Fire Minion
    pushlist 'tames' '776'
elseif skill 'Taming' < 100
    removelist 'tames'
    createlist 'tames'
    //Dragon grey
    pushlist 'tames' '12'
    //Dragon red
    pushlist 'tames' '59'
    //Molten Mongbat
    pushlist 'tames' '39'
    //Wyvern Hatchling
    pushlist 'tames' '733' 
elseif skill 'Taming' < 105
    removelist 'tames'
    createlist 'tames'
    //Snow Drift
    pushlist 'tames' '51'
    //Dusk drake
    pushlist 'tames' '60'
elseif skill 'Taming' < 110
    removelist 'tames'
    createlist 'tames'
    //Earth drake
    pushlist 'tames' '60'
    pushlist 'tames' '61'
    //Acarid
    pushlist 'tames' '385'
elseif skill 'Taming' < 115
    removelist 'tames'
    createlist 'tames'
    //Aegis Minion
    pushlist 'tames' '776'
    //Sphinx
    pushlist 'tames' '293'
elseif skill 'Taming' < 120
    removelist 'tames'
    createlist 'tames'
    //Earth dragon
    pushlist 'tames' '12'
    pushlist 'tames' '59'
    //Colossal Sandroach
    pushlist 'tames' '388'
else
    overhead 'You cannot train beyond 120!'
    pause 1000
    stop
endif
//--------------------------//
//  Find a beast to tame  //
//-------------------------//
foreach 'tame' in 'tames'
    //Kill tamed animal without releasing
    if findtype 'tame' any any any 2 as 'foo'
        overhead "Found a beast!" 555
        //Ignore tameables with the same graphic id
        if skill 'Taming' < 80
            getlabel 'foo' tName
            if 'dragon whelp' in tName
                overhead "Dragon whelp... Ignoring!" 555
                ignore 'foo'
                replay
            endif
        endif
        if noto 'foo' = friend
            overhead "Killing the tamed beast!" 555
            wait 2500
            say 'all kill'
            wait 500
            target 'foo'
            while find 'foo' any any any 2
                if timer 'vet' > 10000
                    hotkey 'cancel current target'
                    overhead "Bandaging!" 555
                    dclicktype 'clean bandage%s%'
                    wait 500
                    target 'tankpet'
                    settimer 'vet' 0
                elseif diffhits > 10
                    overhead "Watch your health!"
                    wait 2000
                else 
                    wait 1000
                endif
            endwhile
        //Tame if creature is criminal or hostile
        elseif noto 'foo' = criminal or noto 'foo' = hostile
           if timer 'taming' > 13000
                overhead "Taming the beast..." 555
                wait 2500
                hotkey 'cancel current target'
                useskill 'taming'
                wait 500
                target 'foo'
                settimer 'taming' 0
                wait 1000
            elseif timer 'taming' < 13000
                overhead "Already taming..." 555
                wait 1000
            endif
        endif
        wait 3000
    else 
        overhead "Searching the area..." 555
        wait 3000
    endif
endfor
loop