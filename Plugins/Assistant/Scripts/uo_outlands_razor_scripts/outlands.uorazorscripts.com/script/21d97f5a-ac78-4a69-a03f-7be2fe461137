# This is an automated backup - check out https://outlands.uorazorscripts.com/script/21d97f5a-ac78-4a69-a03f-7be2fe461137 for latest
# Automation by Jaseowns
## Script: Fishing frenzy
## Created by: Nevor#9051
#############################################

//This script is continously checking for a fishing frenzy within range of sight
//If in range it will automatically start fishing as long as there is a fishing pole equipped or in backpack
//For players with tracking skill it will activate hunting mode and stop if an enemy is detected
//You can input the name of your 'escape script' down below at the '//--->' marked areas if you want to use auto escape with tracking
//Unfortunately it is not possible to discriminate between real and house-deco koi, since they have the same serial and hue

clearsysmsg

say 'Mornin`!' 100
@setvar! speechCycle 1 

//activate hunting mode
if skill 'Tracking' > 0 and not findbuff 'Tracking Hunting'
    skill 'Tracking'
    waitforgump 4267467659
    gumpresponse 6
    waitforgump 4267467659
    gumpclose 4267467659
    wait 200
endif

//main loop, remove this while and the endwhile at end of script in case you do not want the script to cycle
while not dead
    
    //tracking detection check
    if insysmsg 'now tracking'
        overhead '*** RED ALERT ***' 33
//--->  insert the name of your escape script and remove the // below if you want to use auto escape
        //script 'myEscapeScript'
        stop
    endif
    
    //check for a fishing frenzy, standard range of sight is 18 tiles, range for fishing is 8 tiles
    if findtype 'koi' ground -1 -1 18 as myFrenzy
        
        //equip fishing pole if necessary, stop if we do not have one in our backpack
        if lhandempty and findtype 'fishing pole' backpack as myStick
            lift myStick
            drop self lefthand
            while queued
                wait 50
            endwhile
        elseif lhandempty and not findtype 'fishing pole' backpack
            overhead 'No fishing pole' 33
            stop
        endif
        
        //frenzy in range, fishing
        if find myFrenzy ground -1 -1 8
            dclick myFrenzy
            //fishing time for a frenzy is 3 seconds, this for loop can potentially be reduced to 4 if you suffer less lag than I am (there is another 1 sec anti-spam wait below)
            for 5
                //tracking detection check while fishing
                if insysmsg 'now tracking'
                    overhead '*** RED ALERT ***' 33
//--->              insert the name of your escape script and remove the // below if you want to use auto escape
                    //script 'myEscapeScript'
                    stop
                else
                    wait 500
                endif
            endfor
        //frenzy out of range notification
        elseif find myFrenzy ground -1 -1 9
            overhead 'Fishing frenzy 9 tiles' 68
            overhead 'Move closer...' 68
        elseif find myFrenzy ground -1 -1 10    
            overhead 'Fishing frenzy 10 tiles' 58
            overhead 'Move closer...' 58
        elseif find myFrenzy ground -1 -1 11
            overhead 'Fishing frenzy 11 tiles' 56
            overhead 'Move closer...' 56    
        elseif find myFrenzy ground -1 -1 12
            overhead 'Fishing frenzy 12 tiles' 54
            overhead 'Move closer...' 54
        elseif find myFrenzy ground -1 -1 13
            overhead 'Fishing frenzy 13 tiles' 50
            overhead 'Move closer...' 50
        elseif find myFrenzy ground -1 -1 14
            overhead 'Fishing frenzy 14 tiles' 45
            overhead 'Move closer...' 45
        elseif find myFrenzy ground -1 -1 15
            overhead 'Fishing frenzy 15 tiles' 43
            overhead 'Move closer...' 43
        elseif find myFrenzy ground -1 -1 16
            overhead 'Fishing frenzy 16 tiles' 41
            overhead 'Move closer...' 41
        elseif find myFrenzy ground -1 -1 17
            overhead 'Fishing frenzy 17 tiles' 39
            overhead 'Move closer...' 39
        elseif find myFrenzy ground -1 -1 18
            overhead 'Fishing frenzy 18 tiles' 37
            overhead 'Move closer...' 37
        endif
        
        //role playing addition for high quality fishing
        //video suggestion for maximum fishertainment: https://www.youtube.com/watch?v=PEe-ZeVbTLo&t=1984s
        if speechCycle = 0
            say 'Mornin`!' 100
            @setvar! speechCycle 1 
        elseif speechCycle = 1
            say 'Nice day for fishing ain`t it!' 100
            @setvar! speechCycle 2
        elseif speechCycle = 2
            say 'Hu ha!' 100
            @setvar! speechCycle 0
        endif
        
    endif

    //anti-spam wait
    wait 1000
    
    //frenzy done check
    if insysmsg 'The fishing frenzy subsides'
        overhead 'Frenzy done'
    endif

endwhile