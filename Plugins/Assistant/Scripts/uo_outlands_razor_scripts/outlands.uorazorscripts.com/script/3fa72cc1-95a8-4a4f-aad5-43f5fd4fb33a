# This is an automated backup - check out https://outlands.uorazorscripts.com/script/3fa72cc1-95a8-4a4f-aad5-43f5fd4fb33a for latest
# Automation by Jaseowns
## Script: Search Vendor for Specific Item
## Created by: Jaseowns#6694
#############################################

# Search Vendor for Specific Item by Jaseowns
# UO Outlands
# Description: try and search vendors for a specific item. Livestream
# What are we doing?
# - We want to search vendors for a specific item and get the price
# - Starting with a skill orb 
# "name: void orb"
# "id: 22336"
# "hue: 2966"
clearignore

@setvar! item_id_to_buy 22336
@setvar! item_hue_to_buy 2966
@setvar! globalTimeout 650


removelist "playVendors"
createlist "playVendors"

while not dead
    hotkey "Next Monster Target"
    wait 200
    @setvar! playerVendor lasttarget 
    if inlist "playVendors" playerVendor
        // skip
        sysmsg "Might be all done..."
    else
        pushlist "playVendors" playerVendor
        if noto playerVendor = "invulnerable"
            dclick playerVendor
            wait globalTimeout
            

            // 3712 paragon box small
            // 3648 metal chest
            // 3702 bag
            // 3705 pouch
            // 29833 greater paragon
            // 3701 backpack

            for 2
                while findtype 3701|29833|3705|3712|3648|3702 playerVendor as container
                    getlabel container desc
                    if "Price: Not for sale" in desc
                        dclick container
                        wait globalTimeout
                    else
                        sysmsg desc
                    endif
                    ignore container
                endwhile
            endfor

            // 3712 1246
            // 3712 2599

            while findtype item_id_to_buy playerVendor item_hue_to_buy as item
                getlabel item desc
                overhead desc
                wait globalTimeout
                ignore item
            endwhile
        endif
    endif
endwhile