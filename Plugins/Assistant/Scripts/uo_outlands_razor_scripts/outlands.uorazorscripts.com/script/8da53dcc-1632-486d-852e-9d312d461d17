# This is an automated backup - check out https://outlands.uorazorscripts.com/script/8da53dcc-1632-486d-852e-9d312d461d17 for latest
# Automation by Jaseowns
## Script: Mod: Ultimate Lumberjacking Script by Jaseowns V1.0.1 
## Created by: daklue#0
#############################################

##############################################################
# Ultimate Lumberjacking Script by Jaseowns V1.0.1 
# Modified By Daklue for Kernelfault
# UO Outlands
# Requirements: 
#   organizer 1 as lumber and wood planks
#   setup weight checks (default diff weight 30)
##############################################################  
# For Recall with magery = 0 
# For Hiding = 1
# For Recall with runebook but use scroll in book = 2
# For Recall with scroll in backpack on to a rune in backpack = 3
 
@setvar! redsnearby 2

if not varexist jase_lumber_runebook
    overhead "Select your escape runebook" 88
    setvar jase_lumber_runebook
endif

if redsnearby = 3 and not varexist jase_rune
	overhead "Select your escape rune" 88
	setvar jase_rune
endif
	
if not listexists "jase_lumber_actions"
    createlist "jase_lumber_actions"
endif

if inlist "jase_lumber_actions" "red_alert"
    overhead "Red Alert!" 34
    if redsnearby = 0 and skill "Magery" >= 40 
        overhead 'Recalling...'
        while not targetexists
            cast "Recall"
            wft 500
        endwhile
        target jase_lumber_runebook
    elseif redsnearby = 1 and skill "Hiding" >= 40
        useskill "Hiding"
	elseif redsnearby = 2
		overhead 'Recalling with scrolls in runebook...'
		while not targetexists
			dclick jase_lumber_runebook
			waitforgump 1551740969
			gumpresponse 2
			wft 500
		endwhile
	elseif redsnearby = 3
		overhead 'Recalling with a scrolls in backpack on a rune...'
		while not targetexists
            dclick "Recall"
            wft 500
        endwhile
        target jase_rune
    endif
    poplist "jase_lumber_actions" "red_alert"
    overhead "Stopping script..." 34
    stop
    # replay
endif

clearsysmsg 

if diffweight <= 30
    organizer 1
    while queued
        overhead 'Moving items...'
        wait 500
    endwhile
    if diffweight <= 30
        overhead "Too heavy still.. check organizer agent 1" 34
        replay
    endif
endif

if lhandempty ?? 0 
    if findtype "hatchet" backpack
        dclicktype 'hatchet' backpack
        wait 200
    endif
endif
            
if lhandempty ?? 0 
    overhead "No more hatchets!" 34
    replay
endif

wait 500
overhead 'Lumberjacking' 0
hotkey 'Use item in hand'
wft 1000
hotkey 'Target Self'
for 75
    wait 100
    if insysmsg 'world is saving'
        for 30
            overhead 'Waiting for world save...'
            wait 1000
            if insysmsg 'save complete'
                overhead 'Save complete - continue on!' 88
                clearsysmsg 
                wait 250
                replay
            elseif insysmsg "now tracking"
                pushlist "jase_lumber_actions" "red_alert"
                clearsysmsg 
                wait 250
                replay
            endif
        endfor
    elseif insysmsg "now tracking"
        pushlist "jase_lumber_actions" "red_alert"
        replay
    elseif lhandempty ?? 0 
        overhead "Broke axe" 34
        replay
    elseif insysmsg 'You do not see any' or insysmsg 'You cannot produce any wood'
        overhead 'Move to next spot' 88
        wait 250
        replay
    elseif insysmsg "travel"
        overhead 'Waiting for travel...'
        wait 1000
        replay
    else
        if insysmsg "any harvestable" or insysmsg "jase_lumber_actions"
            // No Ore
            overhead 'Move to next spot' 88
            replay
        elseif insysmsg "skillgain" or insysmsg "harvesting is not allowed"
            // Gained skill
            replay
        elseif insysmsg "world is saving" or insysmsg 'World save complete'
            // World Save
            replay
        elseif insysmsg "You hack"
            // Failed
            replay
        elseif insysmsg "You chop"
            // Success
            replay
        elseif insysmsg "You must wait"
            // Wait message
            overhead 'You must wait..' 34
            wait 500
            replay
        endif
    endif
endfor

if insysmsg "You must wait"
    // Wait message
    overhead 'You must wait..' 34
    wait 500
    replay
endif

overhead 'Captcha break!' 34
for 20
    overhead 'Awaiting Captcha...' 34
    wait 1000
    if insysmsg 'Captcha successful'
        overhead 'Success - continue on!' 88
        wait 1000
        replay
    endif
endfor
overhead 'Stopping script' 34