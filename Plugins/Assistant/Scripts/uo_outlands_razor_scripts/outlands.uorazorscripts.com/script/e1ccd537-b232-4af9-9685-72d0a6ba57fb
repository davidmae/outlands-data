# This is an automated backup - check out https://outlands.uorazorscripts.com/script/e1ccd537-b232-4af9-9685-72d0a6ba57fb for latest
# Automation by Jaseowns
## Script: Auto Hide and then Steal near closest mob
## Created by: Jaseowns#6694
#############################################

# Auto Hide and then Steal near closest mob by Jaseowns
# UO Outlands
if targetexists beneficial
    hotkey "Target Self"
    wait 200
elseif targetexists neutral
    hotkey 'Cancel Current target'
endif

hotkey 'Target Closest Non-Friendly Monster'
@setvar! myTarget lasttarget 

if not hidden 
    if skill "Magery" >= 80
        while not targetexists beneficial
            cast "Invisibility"
            wait 50
        endwhile
        while targetexists
            wait 50
            if find myTarget ground -1 -1 1
                replay
            endif
        endwhile
    else
        useskill "hiding"
        wait 200
        replay
    endif
else
    while not targetexists neutral
        useskill "Stealing"
        wait 50
    endwhile
    while targetexists
        if find myTarget ground -1 -1 1
            target myTarget
        else
            overhead "Move closer to steal!" 34
        endif
    endwhile
endif