# Tmap decoder by Jaseowns
# UO Outlands
# Requirements: 
#   organizer agent 1 - set to map and bag/tome for completed maps
#   restock agent 1 - set to 1 map
if not findtype "spyglass" backpack
    overhead "Need spyglass" 34
    wait 5000
    replay
endif

if not varexist "restock_undecoded_maps"
    overhead "Select a bag with undecoded maps" 88
    setvar restock_undecoded_maps
endif

if not findtype "map" backpack
    overhead "Restock Map" 88
    restock 1
    wft 1500
    target "restock_undecoded_maps"
    wait 1000
endif

# Close map gump
gumpclose 1520869851

if findtype "map" backpack as my_map
    getlabel my_map desc
    overhead desc
    
    dclicktype "spyglass" backpack
    wft 1500
    target my_map
    waitforgump 1520869851 1000
    
    if "[100.0%" in desc
        overhead "Moving map..." 88
        organizer 1
        wait 500
        while queued
            overhead "Moving map..." 88
            wait 500
        endwhile
    else
        overhead "Updating..."
        clearsysmsg 
        while gumpexists 1520869851
            gumpresponse 4 1520869851
            wait 500
            if insysmsg "maximum accuracy"
                gumpclose 1520869851
                replay
            endif
        endwhile
    endif
    
    wait 2000
    replay
else
    overhead "Out of maps!" 34
    wait 5000
    replay
endif

