# Bandage Self Hotkey replacement by Jaseowns
# With cure and heal potions
# and stamnia and other checks
# UO Outlands

if not timerexists "aids"
    createtimer "aids"
    settimer "aids" 11000
endif

if not timerexists "healpot"
    createtimer "healpot"
    settimer "healpot" 8000
endif

if stam < maxstam
     if findtype "Red Potion" backpack as pot
        dclick pot
        wait 200
    endif
endif

if poisoned and findtype "Orange Potion" backpack as pot
    dclick pot
    wait 200
endif

if findtype "clean bandage%s%" backpack
    if hp < maxhp or poisoned
        if poisoned and findtype "Orange Potion" backpack and hp < 60
            potion "cure"
            wait 200
        endif
        if hp < 45
            if findtype "Yellow Potion" backpack
                if timer "healpot" >= 8000
                    overhead 'Drinking heal!'
                    potion "heal"
                    wait 200
                    settimer "healpot" 0
                endif
            else 
                overhead 'Out of heals!' 34
            endif
        endif
        if timer "aids" >= 11000
            dclicktype "clean bandage%s%" backpack
            waitfortarget
            hotkey "Target Self"
            wait 200
            if insysmsg "begin applying"
                settimer "aids" 0
            endif
        else 
            overhead "Already running!" 88
        endif
    else 
        overhead "Full health!"
    endif
else
    overhead "No aids!" 34
endif