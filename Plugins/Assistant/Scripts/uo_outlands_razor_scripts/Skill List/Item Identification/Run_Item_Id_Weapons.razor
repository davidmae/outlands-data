# IDing Weapons and saving the best by Jaseowns
# UO Outlands
# Requirements:  
#   Have item ID skill above 0
#
# Additional Info: 
#   This script only saves the following:
#   power
#   vanquishing
#   exceedingly
#   supremely
setvar id_source_container
setvar good_items_store_container

if not listexists id_weapon_items
    createlist id_weapon_items
    pushlist id_weapon_items "martial arts scroll"
    pushlist id_weapon_items "halberd"
    pushlist id_weapon_items "bardiche"
    pushlist id_weapon_items "battle axe"
    pushlist id_weapon_items "double axe"
    pushlist id_weapon_items "two handed axe"
    pushlist id_weapon_items "executioner's axe"
    pushlist id_weapon_items "large battle axe"
    pushlist id_weapon_items "axe"
    pushlist id_weapon_items "scimitar"
    pushlist id_weapon_items "longsword"
    pushlist id_weapon_items "viking sword"
    pushlist id_weapon_items "katana"
    pushlist id_weapon_items "cutlass"
    pushlist id_weapon_items "broadsword"
    pushlist id_weapon_items "war hammer"
    pushlist id_weapon_items "war axe"
    pushlist id_weapon_items "maul"
    pushlist id_weapon_items "quarter staff"
    pushlist id_weapon_items "gnarled staff"
    pushlist id_weapon_items "black staff"
    pushlist id_weapon_items "hammer pick"
    pushlist id_weapon_items "club"
    pushlist id_weapon_items "shepherd's crook"
    pushlist id_weapon_items "war mace"
    pushlist id_weapon_items "mace"
    pushlist id_weapon_items "Long Spear"
    pushlist id_weapon_items "pitchfork"
    pushlist id_weapon_items "war fork"
    pushlist id_weapon_items "kryss"
    pushlist id_weapon_items "short spear"
    pushlist id_weapon_items "bow"
    pushlist id_weapon_items "crossbow"
    pushlist id_weapon_items "heavy crossbow"
    pushlist id_weapon_items "bladed staff"
endif

if skill "Item Identification" <= 0
    overhead "Requires item ID skill to continue.." 34
    stop
endif

foreach id in id_weapon_items
    sysmsg id 88
    while findtype id id_source_container as item
        getlabel item description
        overhead description 88
        if unidentified in description
            useskill "itemId"
            wft 500
            target item
            wait 2000
        elseif power in description or vanquishing in description or supremely in description or exceedingly in description
            lift item
            wait 250
            drop good_items_store_container -1 -1 -1
            ignore item
            wait 250
        else 
            lift item
            wait 250
            drop backpack -1 -1 -1
            ignore item
            wait 250
        endif
    endwhile
endfor

clearignore 