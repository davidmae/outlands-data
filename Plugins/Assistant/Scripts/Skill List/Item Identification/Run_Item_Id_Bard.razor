# IDing Weapons and saving the best by Jaseowns
# UO Outlands
# Requirements:  
#   Have item ID skill above 0
#
# Additional Info: 
#   This script only saves the following:
#   exceedingly
#   supremely
setvar id_source_container
setvar good_items_store_container

if not listexists id_bard_items
    createlist id_bard_items
    pushlist id_bard_items "lap harp"
    pushlist id_bard_items "drum"
    pushlist id_bard_items "lute"
    pushlist id_bard_items "bamboo flute"
    pushlist id_bard_items "tambourine"
endif

if skill "Item Identification" <= 0
    overhead "Requires item ID skill to continue.." 34
    stop
endif

foreach id in id_bard_items
    sysmsg id 88
    while findtype id id_source_container as item
        getlabel item description
        overhead description 88
        if unidentified in description
            useskill "itemId"
            wft 500
            target item
            wait 2000
        elseif supremely in description or exceedingly in description
            lift item
            wait 250
            drop good_items_store_container -1 -1 -1
            ignore item
            wait 250
        else 
            lift item
            wait 250
            drop backpack -1 -1 -1
            ignore item
            wait 250
        endif
    endwhile
endfor

clearignore 