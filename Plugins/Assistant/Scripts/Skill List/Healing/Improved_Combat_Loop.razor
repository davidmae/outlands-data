# Improved Bandage Self Combat Loop by Jaseowns
# UO Outlands
# Disclaimer:  
#   If you are clicking a monster during a heal check,
#   this may target monster and turn you gray.  
#   Use with caution!

if not timerexists "aids"
    createtimer "aids"
    settimer "aids" 16000
endif

if not timerexists "healpot"
    createtimer "healpot"
    settimer "healpot" 10000
endif

clearsysmsg 

while not dead
    
    if insysmsg "You finish applying the bandages"
        settimer "aids" 16000
    elseif insysmsg "but they barely help"
        settimer "aids" 16000
    endif

    if findtype "clean bandage%s%" backpack
        if hp < maxhp
            if hp < 45
                if findtype "Yellow Potion" backpack
                    if timer "healpot" >= 10000
                        overhead 'Drinking heal!'
                        potion "heal"
                        wait 200
                        settimer "healpot" 0
                    endif
                else 
                    overhead 'Out of heals!' 34
                endif
            endif
            if poisoned and hp < 60
                if findtype "Orange Potion" backpack
                    overhead 'Drinking cures!'
                    potion "cure"
                    wait 200
                else 
                    overhead 'Out of cures!' 34
                endif
            endif

            if timer "aids" >= 16000
                hotkey "Bandage Self"
                wait 200
                if insysmsg "begin applying"
                    settimer "aids" 0
                endif
            endif
        endif
    else
        overhead "No aids!" 34
    endif
    wait 200
endwhile 