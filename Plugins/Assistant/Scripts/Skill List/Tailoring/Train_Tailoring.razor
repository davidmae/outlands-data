# Training Tailoring by Jaseowns
# UO Outlands
# Requirements:  145k leather for 100 skill, 260 tailoring kits (store bought)
#   restock agent 5   as      100 leather 2 sewing kits
# Additional Info: 
#   You can use this script OR use the crafting queue
#   To maximize recycling, turn on Recycle Entire Backpack 
if skill 'tailoring' >= 120
    overhead 'Hit max!' 88
    stop
endif

gumpclose 
if count 'leather' < 14 or not findtype "sewing kit" backpack
    clearsysmsg 
    restock 5
    waitfortarget 1500
    target "restock_container_leather"
    wait 2000
    if not insysmsg "Restock request in progress"
        hotkey "Cancel Current Target"
        overhead "Select a container that has your leather and sewing kits (Restock agent 5)" 88
        sysmsg "Jaseowns Tailoring script: Select a container that has your leather and sewing kits" 88
        sysmsg "Jaseowns Tailoring script: (Restock agent 5)" 88
        setvar "restock_container_leather"
        replay
    endif
endif

if not findtype "sewing kit" backpack
    overhead "No sewing kit found!" 34
    wait 1000
    replay
endif

if count 'leather' < 14
    overhead "No leather" 34
    wait 1000
    replay
endif

dclicktype 'sewing kit' backpack
waitforgump 949095101 1000

if skill 'tailoring' < 50
    overhead 'Train at vendor'
    wait 1000
    replay
elseif skill 'tailoring' < 65
    overhead 'Leather Gorget'
    gumpresponse 101
    waitforgump 949095101 1000
    gumpresponse 200
elseif skill 'tailoring' < 70
    overhead 'Leather Gloves'
    gumpresponse 101
    waitforgump 949095101 1000
    gumpresponse 201
elseif skill 'tailoring' < 75
    overhead 'Leather Cap'
    gumpresponse 101
    waitforgump 949095101 1000
    gumpresponse 202
elseif skill 'tailoring' < 85
    overhead 'Studded Gorget'
    gumpresponse 102
    waitforgump 949095101 1000
    gumpresponse 200
elseif skill 'tailoring' < 90
    overhead 'Studded Gloves'
    gumpresponse 102
    waitforgump 949095101 1000
    gumpresponse 201
elseif skill 'tailoring' < 95
    overhead 'Studded Cap'
    gumpresponse 102
    waitforgump 949095101 1000
    gumpresponse 202
elseif skill 'tailoring' < 100
    overhead 'Studded Arms'
    gumpresponse 102
    waitforgump 949095101 1000
    gumpresponse 203
elseif skill 'tailoring' < 105
    overhead 'Studded Legs'
    gumpresponse 102
    waitforgump 949095101 1000
    gumpresponse 204
elseif skill 'tailoring' < 120
    overhead 'Tailoring Repair Kit'
    gumpresponse 108
    waitforgump 949095101 1000
    gumpresponse 203
else
    replay
endif

wait 3000

// Begin Recycle - Recycle Entire Backpack
gumpresponse 3
waitfortarget 1000
target self

loop
