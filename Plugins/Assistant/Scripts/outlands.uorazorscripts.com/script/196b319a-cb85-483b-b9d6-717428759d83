# This is an automated backup - check out https://outlands.uorazorscripts.com/script/196b319a-cb85-483b-b9d6-717428759d83 for latest
# Automation by Jaseowns
## Script: Bless You
## Created by: BrandonFYIA#5248
#############################################

//How often should I bless?
setvar! blessCycle 60000 

//Create Spam Timer
if not timerexists spamTimer
    createtimer spamTimer
    settimer spamTimer 0
endif

//Create List
if not listexists blessList
    createlist  blessList
endif
setvar! mySelf self

//Look for people
for 4
    hotkey 'Next Player Target'
    @setvar! myTarget lasttarget 
    if inlist blessList myTarget
        //do nothing
        sysmsg 'Already In List'
    elseif myTarget != mySelf
        pushlist blessList myTarget
        sysmsg 'Adding to List'
    endif
endfor

poplist blessList mySelf

if timer spamTimer > blessCycle
    //Restock
    if @findtype "storage shelf" ground any 1 2 as 'Shelf'
        menu 'Shelf' 1
    endif
     //Bless each player
    foreach pName in blessList
        cast 'Bless'
        wait 2000
        target pName
    endfor
     //Cast Arch protect
    if list blessList > 0
        say 'Blessings to you nerds!'
        cast 'Arch Protection' 
        wait 2000
        target self
    endif
    //Clear List
    clearlist blessList
    settimer spamTimer 0
endif
loop 