# This is an automated backup - check out https://outlands.uorazorscripts.com/script/65150068-2eb7-455d-9e42-90092bc09f5b for latest
# Automation by Jaseowns
## Script: Stack gold into 60k piles
## Created by: initsu#0
#############################################

// selecting the container ID to use
// leave it empty to pick a container each time
// getting bank container ID: type -info and click on anything in your bank
// getting other container ID: type >info and click on the container
setvar! stackContainer

@setvar! stack30kPile 0
@setvar! stackSmallPile 0
@clearignore
while findtype "gold coin" stackContainer as goldPile
    getlabel goldPile desc
    if ": 60000" in desc
        @ignore goldPile
    elseif ": 30000" in desc
        if stack30kPile == 0
            @setvar! stack30kPile goldPile
            @ignore goldPile
        else
            lift goldPile 30000
            drop stack30kPile
            @setvar! stack30kPile 0
            wait 750
        endif
    else
        lift goldPile 30000
        drop stackContainer
        wait 750
        getlabel goldPile desc
        if ": 30000" in desc
        elseif stackSmallPile == 0
            @setvar! stackSmallPile goldPile
            @ignore goldPile
        else
            lift goldPile 60000
            drop stackSmallPile
            @unignore stackSmallPile
            @setvar! stackSmallPile 0
            wait 750
        endif
    endif
endwhile

if stack30kPile != 0 and stackSmallPile != 0
    lift stackSmallPile 60000
    drop stack30kPile
    wait 750
endif
overhead "Done"
