# This is an automated backup - check out https://outlands.uorazorscripts.com/script/48902e43-fd1f-4441-9030-d23b580eec7c for latest
# Automation by Jaseowns
## Script: Vendor Scavenger
## Created by: AreYouKidden=2#0403
#############################################

@setvar 'PauseTime' 750
if not listexists 'ScavItems'
    createlist 'ScavItems'
endif
clearlist 'ScavItems'
//Reagents
pushlist 'ScavItems' '0xf7b' 'back'
pushlist 'ScavItems' '0xf7a' 'back'
pushlist 'ScavItems' '0xf86' 'back'
pushlist 'ScavItems' '0xf8c' 'back'
pushlist 'ScavItems' '0xf85' 'back'
pushlist 'ScavItems' '0xf84' 'back'
pushlist 'ScavItems' '0xf8d' 'back'
pushlist 'ScavItems' '0xf88' 'back'
//Bottles, Scrolls, Arrows, Bolts
pushlist 'ScavItems' '0xf0e' 'back'
pushlist 'ScavItems' '0xef3' 'back'
pushlist 'ScavItems' '0xf3f' 'back' 
pushlist 'ScavItems' '0x1bfb' 'back'
//Bolts of Cloth
pushlist 'ScavItems' '0xf95' 'back'
if not listexists 'ScavAnimals'
    createlist 'ScavAnimals'
endif
foreach x in 'ScavAnimals'
   if not find 'x'
    overhead 'ScavAnimal List cleared'
    clearlist 'ScavAnimals'
   endif
endfor
pause 250
if list 'ScavAnimals' < 1
    overhead 'Scanning for PackAnimals'
    while findtype 0x0123|0x0124 'ground' 'any' 'any' 2 as 'found' 
        pushlist 'ScavAnimals' 'found' front
        ignore 'found'
    endwhile
    clearignore
endif
foreach 'x' in 'ScavItems'
    while findtype 'x' ground any any 2 as groundItem
        foreach 'y' in 'ScavAnimals'
            @setvar 'CurrPacky' 'y'
            break
        endfor
        @poplist 'ScavAnimals' 'CurrPacky'
        @pushlist 'ScavAnimals' 'CurrPacky' 'back'

        lift groundItem 9999
        overhead 'CurrPacky'
        drop 'CurrPacky' -1 -1 0
        wait 'PauseTime'
    endwhile
endfor
foreach 'x' in 'ScavItems'
    while findtype 'x' backpack any any 1 as groundItem
        foreach 'y' in 'ScavAnimals'
            @setvar 'CurrPacky' 'y'
            break
        endfor
        @poplist 'ScavAnimals' 'CurrPacky'
        @pushlist 'ScavAnimals' 'CurrPacky' 'back'

        lift groundItem 9999
        overhead 'CurrPacky'
        drop 'CurrPacky' -1 -1 0
        wait 'PauseTime'
    endwhile
endfor